;; -*- mode: wat -*-

(namespace $list<*>
  (type T (struct (field (ref null any)) (field (ref null T))))
  (func cons (param $head (ref null any)) (param $tail (ref null T)) (result (ref T))
    (struct.new T $head $tail))
  (func head (param $pair (ref null T)) (result (ref null any))
    (struct.get T 0 $pair))
  (func tail (param $pair (ref null T)) (result (ref null T))
    (struct.get T 1 $pair))

  (func len/acc (param $l (ref null T)) (param $acc i32) (result i32)
    (if (ref.is_null $l)
      (then (return $acc)))
    (return_call len/acc (struct.get T 1 $l) (+ $acc 1)))
  (func len (param $l (ref null T)) (result i32)
    (return_call len/acc $l 0))

  (func revAppend (param $l1 (ref null T)) (param $l2 (ref null T)) (result (ref null T))
    (if (ref.is_null $l1)
      (then (return $l2)))
    (return_call revAppend
      (struct.get T 1 $l1)
      (struct.new T (struct.get T 0 $l1) $l2)))

  (func rev (param $l (ref null T)) (result (ref null T))
    (return_call revAppend $l (ref.null T)))

  (func sumToI64/acc (param $l (ref null T)) (param $clo (ref $Closure.Base)) (param $acc i64) (result i64)
    (if (ref.is_null $l)
      (then (return $acc)))
    (call sumToI64/acc
      (struct.get T 1 $l)
      $clo
      (i64 (+ $acc (call_closure (param (ref null any)) (result i64) $clo (struct.get T 0 $l))))))
  (func sumToI64 (param $l (ref null T)) (param $clo (ref $Closure.Base)) (result i64)
    (return_call sumToI64/acc $l $clo (i64 0))))
