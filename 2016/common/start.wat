(table $mains 26 funcref)

(global $start.maxSlowness i32 (i32.const 2))
;; Names chosen for visual length
(global $start.fast i32 (i32.const 0))
(global $start.bitSlow i32 (i32.const 1))
(global $start.reallySlow i32 (i32.const 2))

(func (export "_start")
  (local $err i32)

  (i32.const 0)
  (i32.add (call $start.runDay (i32.const 01) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 02) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 03) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 04) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 05) (global.get $start.reallySlow)))
  (i32.add (call $start.runDay (i32.const 06) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 07) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 08) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 09) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 10) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 11) (global.get $start.reallySlow)))
  (i32.add (call $start.runDay (i32.const 12) (global.get $start.bitSlow)))
  (i32.add (call $start.runDay (i32.const 13) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 14) (global.get $start.reallySlow)))
  (i32.add (call $start.runDay (i32.const 15) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 16) (global.get $start.bitSlow)))
  (i32.add (call $start.runDay (i32.const 17) (global.get $start.bitSlow)))
  (i32.add (call $start.runDay (i32.const 18) (global.get $start.bitSlow)))
  (i32.add (call $start.runDay (i32.const 19) (global.get $start.reallySlow)))
  (i32.add (call $start.runDay (i32.const 20) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 21) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 22) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 23) (global.get $start.reallySlow)))
  (i32.add (call $start.runDay (i32.const 24) (global.get $start.fast)))
  (i32.add (call $start.runDay (i32.const 25) (global.get $start.fast)))
  (local.set $err)

  (call $printI32.nl (local.get $err))
  (call $proc_exit (local.get $err)))

(data (i32.const 0x1a_0000) "=== day XX ===")
(data (i32.const 0x1a_0010) ".XXXs")
(data (i32.const 0x1a_0020) "marked as slow... skipping")

(func $start.header (param $day i32)
  (i32.store8 (i32.const 0x1a_0008)
    (i32.add (i32.const 0x30) (i32.div_u (local.get $day) (i32.const 10))))
  (i32.store8 (i32.const 0x1a_0009)
    (i32.add (i32.const 0x30) (i32.rem_u (local.get $day) (i32.const 10))))
  (call $printStr.nl (i32.const 0x1a_0000)))

(func $start.runDay (param $day i32) (param $slowness i32) (result i32)
  (local $startTime i64)
  (local $endTime i64)
  (local $result i32)
  (local $time i64)
  (local $mtime i64)

  (call $start.header (local.get $day))

  (if (i32.gt_s (local.get $slowness) (global.get $start.maxSlowness))
    (then
      (call $printStr.nl (i32.const 0x1a_0020))
      (call $print.nl)
      (return (i32.const 0))))

  (local.set $startTime (call $getClock))
  (local.set $result (call_indirect $mains (result i32) (local.get $day)))
  (local.set $endTime (call $getClock))

  (local.set $mtime (i64.div_u (i64.sub (local.get $endTime) (local.get $startTime)) (i64.const 1_000_000)))
  (local.set $time (i64.div_u (local.get $mtime) (i64.const 1_000)))
  (local.set $mtime (i64.rem_u (local.get $mtime) (i64.const 1_000)))
  (i64.store8 (i32.const 0x1a_0013)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (local.set $mtime (i64.div_u (local.get $mtime) (i64.const 10)))
  (i64.store8 (i32.const 0x1a_0012)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (local.set $mtime (i64.div_u (local.get $mtime) (i64.const 10)))
  (i64.store8 (i32.const 0x1a_0011)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (call $printI64 (local.get $time))
  (call $printStr.nl (i32.const 0x1a_0010))
  (call $print.nl)
  (local.get $result))
