(table $mains 26 funcref)

(global $start.runSlow i32 (i32.const 0))

(func (export "_start")
  (local $err i32)

  (i32.const 0)
  (i32.add (call $start.runDay (i32.const 01)))
  (i32.add (call $start.runDay (i32.const 02)))
  (i32.add (call $start.runDay (i32.const 03)))
  (i32.add (call $start.runDay (i32.const 04)))
  (i32.add (call $start.slowDay (i32.const 05)))
  (i32.add (call $start.runDay (i32.const 06)))
  (i32.add (call $start.runDay (i32.const 07)))
  (i32.add (call $start.runDay (i32.const 08)))
  (i32.add (call $start.runDay (i32.const 09)))
  (i32.add (call $start.runDay (i32.const 10)))
  (i32.add (call $start.slowDay (i32.const 11)))
  (i32.add (call $start.runDay (i32.const 12)))
  (i32.add (call $start.runDay (i32.const 13)))
  (i32.add (call $start.slowDay (i32.const 14)))
  (i32.add (call $start.runDay (i32.const 15)))
  (i32.add (call $start.runDay (i32.const 16)))
  (i32.add (call $start.runDay (i32.const 17)))
  (i32.add (call $start.runDay (i32.const 18)))
  (i32.add (call $start.slowDay (i32.const 19)))
  (i32.add (call $start.runDay (i32.const 20)))
  ;; (i32.add (call $start.runDay (i32.const 21)))
  ;; (i32.add (call $start.runDay (i32.const 22)))
  ;; (i32.add (call $start.runDay (i32.const 23)))
  ;; (i32.add (call $start.runDay (i32.const 24)))
  ;; (i32.add (call $start.runDay (i32.const 25)))
  (local.set $err)

  (call $printI32.nl (local.get $err))
  (call $proc_exit (local.get $err)))

(data (i32.const 0x1a_0000) "=== day XX ===")
(data (i32.const 0x1a_0010) ".XXXs")
(data (i32.const 0x1a_0020) "marked as slow... skipping")

(func $start.header (param $day i32)
  (i32.store8 (i32.const 0x1a_0008)
    (i32.add (i32.const 0x30) (i32.div_u (local.get $day) (i32.const 10))))
  (i32.store8 (i32.const 0x1a_0009)
    (i32.add (i32.const 0x30) (i32.rem_u (local.get $day) (i32.const 10))))
  (call $printStr.nl (i32.const 0x1a_0000)))

(func $start.slowDay (param $day i32) (result i32)
  (if (global.get $start.runSlow)
    (then (return_call $start.runDay (local.get $day))))
  (call $start.header (local.get $day))
  (call $printStr.nl (i32.const 0x1a_0020))
  (call $print.nl)
  (i32.const 0))

(func $start.runDay (param $day i32) (result i32)
  (local $startTime i64)
  (local $endTime i64)
  (local $result i32)
  (local $time i64)
  (local $mtime i64)

  (call $start.header (local.get $day))

  (local.set $startTime (call $getClock))
  (local.set $result (call_indirect $mains (result i32) (local.get $day)))
  (local.set $endTime (call $getClock))

  (local.set $mtime (i64.div_u (i64.sub (local.get $endTime) (local.get $startTime)) (i64.const 1_000_000)))
  (local.set $time (i64.div_u (local.get $mtime) (i64.const 1_000)))
  (local.set $mtime (i64.rem_u (local.get $mtime) (i64.const 1_000)))
  (i64.store8 (i32.const 0x1a_0013)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (local.set $mtime (i64.div_u (local.get $mtime) (i64.const 10)))
  (i64.store8 (i32.const 0x1a_0012)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (local.set $mtime (i64.div_u (local.get $mtime) (i64.const 10)))
  (i64.store8 (i32.const 0x1a_0011)
    (i64.add (i64.const 0x30) (i64.rem_u (local.get $mtime) (i64.const 10))))
  (call $printI64 (local.get $time))
  (call $printStr.nl (i32.const 0x1a_0010))
  (call $print.nl)
  (local.get $result))
